buildscript {
    repositories {
        google()
        jcenter()
    }
    ext.kotlinVersion = '1.4.10'
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.android.tools.build:gradle:3.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

repositories {
    google()
    jcenter()
    if (project.hasProperty('devReadUser')) {
        maven {
            url "http://jfrog.high-mobility.com/artifactory/gradle-dev-local/"
            credentials {
                username devReadUser
                password devReadPassword
            }
        }
    }
}

ext.ver = [
        "hmkit-android"     : "2.0.5",
        "hmkit-auto-api"    : "3.12.0",
        "command-queue"     : "1.1.1",
        compileSdk          : 28
]

ext.deps = [
        "hmkit"   : "com.highmobility:hmkit-android:${ver."hmkit-android"}@aar",
        "autoApi" : "com.highmobility:hmkit-auto-api:${ver."hmkit-auto-api"}",
        "timber"  : "com.jakewharton.timber:timber:4.7.1",
        "volley"  : "com.android.volley:volley:1.1.1",
        "findBugs": 'com.google.code.findbugs:jsr305:3.0.2'
]

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "deploy.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/deploy-android-v2.gradle"

android {
    compileSdkVersion ver.compileSdk
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion ver.compileSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    if (project.hasProperty('depLocation') == false || project.depLocation > 0) {
        implementation(deps.hmkit) {
            transitive = true
        }
        implementation deps.autoApi
    } else {
        implementation project(path: ':hmkit-android')
        implementation project(path: ':hmkit-crypto')
        implementation project(path: ':hmkit-utils')
        implementation project(path: ':hmkit-auto-api')
    }

    implementation deps.findBugs
    implementation deps.timber

    testImplementation "at.favre.lib:slf4j-timber:1.0.0"
    testImplementation 'junit:junit:4.12'
}