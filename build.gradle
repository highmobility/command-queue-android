buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.13.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'digital.wup:android-maven-publish:3.6.3'
    }
}

repositories {
    google()
    jcenter()
    maven { url "http://jfrog.high-mobility.com/artifactory/gradle-release-local/" }
}

// these are not release numbers. release numbers are in deploy.settings files.
ext.ver = [
        hmkit     : "2.0.2",
        autoApi   : "3.11.1",
        compileSdk: 29
]

ext.deps = [
        "hmkit"   : "com.highmobility:hmkit-android:${ver.hmkit}@aar",
        "autoApi" : "com.highmobility:hmkit-auto-api:${ver.autoApi}",
        "timber"  : "com.jakewharton.timber:timber:4.7.1",
        "findBugs": 'com.google.code.findbugs:jsr305:3.0.2'
]

apply plugin: 'com.android.library'

def deploy = new Properties()
deploy.load(new FileInputStream("$projectDir/deploy.settings"))

version deploy.version

android {
    compileSdkVersion ver.compileSdk
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion ver.compileSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    if (project.hasProperty('depLocation') == false || project.depLocation > 0) {
        implementation(deps.hmkit) {
            transitive = true
        }
        implementation deps.autoApi

    } else {
        implementation project(path: ':hmkit-android')
        implementation project(path: ':hmkit-crypto')
        implementation project(path: ':hmkit-utils')
        implementation project(path: ':hmkit-auto-api')
    }

    implementation deps.findBugs
    implementation deps.timber

    testImplementation "at.favre.lib:slf4j-timber:1.0.0"
    testImplementation 'junit:junit:4.12'
}

// deploy script
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/scripts/deploy-android.gradle"